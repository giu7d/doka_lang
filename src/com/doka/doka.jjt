options {
  STATIC = true;
}

PARSER_BEGIN(Doka)

import javax.swing.*;
import java.io.*;

public class Doka {

    private static Boolean displayTree = false;

    public static void main(String args[]) throws ParseException {

        System.out.println("------------------------");
        System.out.println("\") Doka Language ");
        System.out.println("\tv0.0.1");
        System.out.println("------------------------\n");

        if (args.length == 0) {
            System.out.println(
                "doka <params>\n\n" +
                "Examples:\n" +
                "\tdoka ./example.dk\tUse path to load file\n" +
                "\tdoka -i\t\t\tUse GUI to load file\n" +
                "\tdoka -c\t\t\tUse interpreter mode\n" +
                "\tdoka -t <params>\tDisplay parse tree\n" +
                "\tdoka -t ./example.dk\tDisplay parse tree in file mode\n" +
                "\tdoka -t -c\t\tDisplay parse tree in interpreter mode\n" +
                "\tdoka -t -i\t\tDisplay parse tree in GUI mode\n"
            );
            return;
        }

        try {
            for (String arg : args) {
                if (arg.equals("-t")) {
                    System.out.println(">\tUsing display parse tree [-t]");
                    displayTree = true;
                } else if (arg.equals("-c")) {
                    System.out.println(">\tUsing CLI interpreter [-c]");
                    parseInputStream();
                } else if (arg.equals("-i")) {
                    System.out.println(">\tUsing interface [-i]");
                    String path = Doka.getFilePathFromUI();
                    parseFileStream(path);
                } else if (arg.matches("(.*).dk")) {
                    System.out.printf(">\tUsing File [%s]\n", arg);
                    File file = new File(arg);
                    parseFileStream(file.getAbsolutePath());
                } else {
                    System.out.println(
                        "doka <params>\n\n" +
                        "Examples:\n" +
                        "\tdoka ./example.dk\tUse path to load file\n" +
                        "\tdoka -i\t\t\tUse GUI to load file\n" +
                        "\tdoka -c\t\t\tUse interpreter mode\n" +
                        "\tdoka -t <params>\tDisplay parse tree\n"
                    );
                }
            }
        } catch (Exception e) {
            System.out.println(":Error");
            System.out.println(e.getMessage());
        }
    }

    public static void parseFileStream(String path) {
        try {
            System.out.println(">\tParsing...");
            BufferedReader fileBuffer = Doka.getFileBuffer(path);
            Doka parser = new Doka(fileBuffer);
            SimpleNode tree = parser.Main();
            showTree(tree);
        } catch (ParseException e) {
            System.out.println(":Error:Parser");
            System.out.println(e.getMessage());
        } catch (Exception e) {
            System.out.println(":Error");
            System.out.println(e.getMessage());
        }
    }

    public static void parseInputStream() {
        Doka parser = new Doka(System.in);
        System.out.println(">\tListen for input...\n");

        while (true) {
            try{
                SimpleNode tree = parser.Main();
                showTree(tree);
            } catch (Exception e) {
                System.out.println(":Error");
                System.out.println(e.getMessage());
                Doka.ReInit(System.in);
            }
        }
    }

    public static void showTree(SimpleNode tree) {
        if (displayTree) {
            System.out.println(">\tParse tree");
            tree.dump("\t|\t");
        }
    }

    public static BufferedReader getFileBuffer(String path){
        BufferedReader buffer;

        try {

            System.out.println(">\tReading File");
            File file  = new File(path);
            FileReader reader = new FileReader(file);
            buffer = new BufferedReader(reader);
        } catch (IOException e) {
            buffer = null;
            System.out.println(":Error:IO:getFileBuffer");
            System.out.println(e.getMessage());
        }

        return buffer;
    }

    public static String getFilePathFromUI() {
        JFileChooser chooseFile = new JFileChooser();
        chooseFile.showOpenDialog(null);
        String path = chooseFile.getSelectedFile().getAbsolutePath();
        System.out.println(">\tLoad file: " + path);
        return path;
    }

}

PARSER_END(Doka)

SKIP : {
    " "  | "\t"
}

// Reserved Words
TOKEN : {
    <MODULE: "module">
|   <EXIT: "exit">
|   <FUNCTION: "fun">
|   <IF: "if">
|   <ELSE: "else">
|   <ELSEIF: "else if">
}

// Modules
TOKEN : {
    <IO_MODULE: "IO">
|   <LIST_MODULE: "List">
}

// Operators
TOKEN : {
    <AND: "&&">
|   <OR: "||" >
| 	<SUM: "+">
|	<SUBTRACT: "-">
|	<MULTIPLY: "*">
|	<DIVIDE: "/">
|	<REST: "%">
|	<ASSIGN: "=">
|	<GREATER: ">" >
|	<SMALLER: "<">
|	<GREATER_OR_EQUAL: "<=" >
|	<SMALLER_OR_EQUAL: ">=" >
|	<DIFFERENT: "!=">
|	<EQUAL: "==">
|	<PIPE: "|>">
}

// Symbols
TOKEN : {
    <UNDERLINE: "_">
|   <DOT: ".">
|   <COMMA: ",">
|   <EOL: "\n" | "\r" | "\r\n" >
|   <ARROW: "->">
|   <RETURN_ARROW: "<-">
| 	<QUOTE: "\"">
|   <START_BRACKET: "[">
|   <END_BRACKET: "]">
|   <START_PARAM: "(">
|   <END_PARAM: ")">
|   <START_BLOCK: ":">
|   <END_BLOCK: "end">
|   <#DIGIT: ["0"-"9"]>
|   <#UPPER_CASE_CHAR: ["A"-"Z"]>
|   <#LOWER_CASE_CHAR: ["a"-"z"]>
|   <#ID_CHAR: (<LOWER_CASE_CHAR> | <UPPER_CASE_CHAR> | <UNDERLINE> )>
}

// IDs
TOKEN : {
    <MODULE_ID: (<UPPER_CASE_CHAR>(<UPPER_CASE_CHAR> | <LOWER_CASE_CHAR> | <DIGIT>)*)>
|   <GENERIC_ID: (<ID_CHAR> (<ID_CHAR> | <DIGIT>)*)>
}

// Types
TOKEN : {
	 <NUMBER: (<DIGIT>)+( "." (<DIGIT>)+)?>
|    <STRING: (<QUOTE> (~["\""])* <QUOTE>)>
|    <BOOLEAN: ("true" | "false")>
}


SimpleNode Main() :
{}
{
    (<EOF>|<EXIT><EOL>) {
        return jjtThis;
    }
|    <MODULE> <MODULE_ID> <EOL>
        (block() | <EOL>)*
    (<EOF>|<EXIT><EOL>) {
        return jjtThis;
    }
}

// Blocks

void block():
{}
{
    declaration_statements()
|   module_statements()
}

void functinal_declaration_block():
{}
{
    (block()(<EOL>)*)*
    (declare_condition() | (<RETURN_ARROW> (return_expression()(<EOL>)*)+))
}

// Statements
void declaration_statements():
{}
{
    <GENERIC_ID> (declare_assigment() | callable_function_statements())
|   declare_function()
}

void module_statements():
{}
{
    <LIST_MODULE> <DOT> <GENERIC_ID> (declare_function_params())+
|   <IO_MODULE> <DOT> <GENERIC_ID> (declare_function_params())+
}

void callable_function_statements():
{}
{
    (declare_function_params())+
}

// Declarations

void declare_assigment():
{}
{
    <ASSIGN> (expression())+ <EOL>
}

void declare_condition():
{}
{
  <IF> declare_conditional_params() <START_BLOCK> (<EOL>)*
    functinal_declaration_block()(<EOL>)*
  (<ELSEIF> declare_conditional_params() <START_BLOCK> (<EOL>)*
    functinal_declaration_block()(<EOL>)*
  )*
  (<ELSE> <START_BLOCK> (<EOL>)*
    functinal_declaration_block()(<EOL>)*
  )?
  <END_BLOCK> <EOL>
}

void declare_lambda_function():
{}
{
    <START_BLOCK> declare_function_params() <ARROW>(<EOL>)*
        return_expression()(<EOL>)*
    <END_BLOCK>
}

void declare_function():
{}
{
    <FUNCTION> <GENERIC_ID> declare_function_params() <START_BLOCK> (<EOL>)+
       functinal_declaration_block()
    <END_BLOCK> <EOL>
}

void declare_list():
{}
{
   <START_BRACKET> (list_expression())* <END_BRACKET>
}

void declare_conditional_params():
{}
{
   <START_PARAM>expression()(logic_operator()expression())*<END_PARAM>
}

void declare_function_params():
{}
{
   <START_PARAM> (list_expression())* <END_PARAM>
}

// Expressions

void return_expression():
{}
{
    expression()
|   <PIPE> return_expression()
}

void expression():
{}
{
    declare_lambda_function()
|   declare_list()
|   module_statements()
|   <NUMBER> (mathematical_expression() | logical_expression())?
|   (<STRING> | <BOOLEAN>) (logical_expression())?
|   <GENERIC_ID> (right_side_expression())*
|   priority_expression()(right_side_expression())?
}

void right_side_expression():
{}
{
    callable_expression()
|   logical_expression()
|   mathematical_expression()
}

void callable_expression():
{}
{
    (breckets_expression())+
|   callable_function_statements()
}

void logical_expression():
{}
{
  logic_operator() expression()
}

void mathematical_expression():
{}
{
  math_operator() expression()
}

void priority_expression():
{}
{
    <START_PARAM> expression() <END_PARAM>
}

void breckets_expression():
{}
{
   <START_BRACKET> expression() <END_BRACKET>
}

void list_expression():
{}
{
    expression() (<COMMA> expression())*
}

// Operators
void math_operator():
{}
{
    <SUM>
|   <SUBTRACT>
|   <MULTIPLY>
|   <DIVIDE>
|   <REST>
}

void logic_operator():
{}
{
	<AND>
|	<OR>
|   <GREATER>
|	<SMALLER>
|	<GREATER_OR_EQUAL>
|	<SMALLER_OR_EQUAL>
|	<DIFFERENT>
|	<EQUAL>
}




